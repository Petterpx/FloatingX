plugins {
    alias(libs.plugins.android.lirary)
    alias(libs.plugins.vanniketch.maven.publish)
    alias(libs.plugins.jetbrains.kotlin.android)
}

mavenPublishing {
    def versionName = project.hasProperty("versionName") ? project.getProperty("versionName") : null
    def isPublish = project.hasProperty("isPublish") ? project.getProperty("isPublish").toBoolean() : false
    if (isPublish) signAllPublications()
    coordinates("io.github.petterpx", "floatingx", versionName)
}

android {
    namespace 'com.petterp.floatingx'

    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        consumerProguardFiles "proguard-floatingx.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-floatingx.pro'
        }
    }
    compileOptions {
        def javeVersion = JavaVersion.toVersion(libs.versions.jvmTarget.get())
        sourceCompatibility javeVersion
        targetCompatibility javeVersion
    }
    kotlinOptions {
        jvmTarget = libs.versions.jvmTarget.get()
    }
    buildFeatures {
        buildConfig = false
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.kotlin.stdlib
}